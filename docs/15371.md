# 使用 PegJS 编写 DSL 解析器

> 原文 https://dev.to/barryosull/writing-a-dsl-parser-using-pegjs-4 IGO

在[上一篇文章](https://dev.to/barryosull/write-dsls-and-code-faster-20me)中，我写了领域特定语言(DSL)以及它们有多有用，但是我没有深入解析它们的细节，这就是本文的来源。

之前我们制作了这个 DSL:

```
User.ScheduleAppointment has { 
  a UserId userId 
  an AppointmentDatetime appointmentDatetime
  a Location location from {
    a LocationName locationName from location
    a Latitude latitude
    a Longitude longitude
  }
} 
```

Enter fullscreen mode Exit fullscreen mode

我们想获取上面的内容，并对其进行解析。这意味着将文本转换成抽象语法树(AST)。AST 是一个易于导航和解释的树形结构。一旦我们有了 AST，我们就可以解释它。

为此，我们将使用 [PegJS](http://pegjs.org/) ，一个用 Javascript 编写的解析表达式语法(PEG)解析器。PegJS(像大多数解析器一样)是基于正则表达式的，它们允许您构建命名的正则表达式(规则)，您可以将它们组合在一起形成一棵树。规则的结果可以转化为数据结构，让您构建 AST。

# AST

我们需要做的第一件事是设计我们的 AST，因为我们需要知道最终的结果是什么样的。一旦我们知道这一点，我们就可以逆向工程的规则。

所以在我们的完美世界里，我们想把上面的 DSL 变成下面的。

```
{  "entity":  "User",  "command":  "ScheduleAppointment",  "values":  [  {  "class":  "UserId",  "param":  "userId",  "alias":  null,  "type":  "value"  },  {  "class":  "AppointmentDatetime",  "param":  "appointmentDatetime",  "alias":  null,  "type":  "value"  },  {  "class":  "Location",  "param":  "location",  "type":  "composite",  "values":  [  {  "class":  "LocationName",  "param":  "locationName",  "alias":  "location",  "type":  "value"  },  {  "class":  "Latitude",  "param":  "latitude",  "alias":  null,  "type":  "value"  },  {  "class":  "Longitude",  "param":  "longitude",  "alias":  null,  "type":  "value"  }  ]  }  ]  } 
```

Enter fullscreen mode Exit fullscreen mode

这种结构易于导航和解释，因此它是我们的解析器的最终目标。现在我们知道我们想要什么了，让我们想想怎么去那里。

# 书写钉住

peg 通过正则表达式工作。如果您和我一样，那么您的 regex-fu 可能有点弱，所以编写一个解析器似乎是一项令人生畏的任务。谢天谢地，学习正则表达式有一些简单的方法。我推荐玩这个 [regex 纵横字谜游戏](https://regexcrossword.com/)。一旦你完成了“有经验的”水平的纵横字谜游戏，你就会很好地理解正则表达式，你将能够不用查阅正则表达式文档就能编写一个解析器。我强烈推荐这个游戏给任何想学习正则表达式的人。

假设我们理解正则表达式，这里有一个简单规则的例子。

## 样本 PegJS 规则

```
Var = name:[A-Za-z0-9_]*
  {
    return name.join("");
  } 
```

Enter fullscreen mode Exit fullscreen mode

上面是一个匹配变量名的 PegJS 规则，比如下面的“positionId”、“canidateId”、“variable_name”等等...。然后它将结果作为字符串返回。这里这被定义为一个叫做`Var`的“规则”,可以在整个解析器中重用，这样我们就不必重复代码，使得解析器更容易阅读和使用。

## 规则

PegJS 解析器由规则组成。我们的目标是利用上面的 DSL，找出每种 AST 结构的规则。规则是可组合的，所以一旦我们有了一些基本规则，我们就可以开始构建更复杂的规则。

1.  空白(_):匹配所有空格、换行符和制表符，通常被忽略
2.  Var:匹配有效的变量名
3.  Alias:请求参数的别名(Var ),可选
4.  值:由类(Var)、参数名(Var)和可选别名(Var)组成
5.  CompositeValue:一个类(Var)、一个参数名(Var)和一组值的组合
6.  值:值和复合值的集合
7.  命令:实体(Var)、命令(Var)和值(values)集合的组合

现在我们知道了对象类型，我们可以编写 PEGJs 规则来解析 DSL 并创建 AST。

# 全力盯住

```
/**********
 Starting rule, our entry point to the parser.
 The first part of the PEG extracts the entity name as a string, 
 and makes the "entity" accessible in the JS below, allowing us to the AST and return it. 
 It matches certain expected keywords, eg. "has", but does nothing with them. 
 Finally it extracts the values, which have already been turned into a Values AST. 
 You'll see the "_" rule used here, this means "accept/skip whitespace", it's defined below.
**********/
Command = entity:Var "." command:Var _ "has" _ "nothing"* _ values:Values* _
  {
    // Handle case that there are no values
    values = values.length === 0 ? [] : values[0];

    // Return the matched results with this object structure
    return {
      entity: entity,
      command: command,
      values: values
    }
  }

/**********
 Matches a collection of inputs, 0 to many, that are wrapped in parentheses
**********/
Values = "{" _ values:(CompositeValue/Value)* _ "}"
  {
    return values;
  }

/**********
 Value and CompositeValues always have the same initial shape, so I extracted 
 this into a partial result that is extended by both Value and Composite
**********/
ValuePartial = _ "a" [n]? _ cls:Var _ name:Var _
  {
    return {
      class: cls,
      param: name
    }
  }

Value = value:ValuePartial alias:(Alias)? _
  {
    value.requestParam = (alias) ? alias: value.param;
    value.type = 'value';
    return value;
  }

/**********
 Extract the alias value, ignore the "from" string
**********/   
Alias = _ "from" _ alias:Var 
  {
    return alias;
  }

CompositeValue = value:ValuePartial "from" _ values:Values
  {
    value.type = 'composite';
    value.values = values;
    return value;
  }

Var = name:[A-Za-z0-9_]*
  {
    return name.join("");
  }

/**********
 Match any sequence of "whitespace" characters
**********/   
_ = [ \t\n\r]* 
```

Enter fullscreen mode Exit fullscreen mode

这就是完整的解析器。上面会把我们的 DSL 变成上面的 AST。你可以自己检查一下。只需进入 PegJS，打开他们的[在线编辑器](http://pegjs.org/online)，粘贴上面的 DSL 和解析器。你会马上看到结果。

顺便提一下，我们正在使用一个输出 PHP 版本解析器的 PegJS 扩展，因此我们可以在服务器和客户机上使用同一个解析器。

## 结论

如您所见，编写解析器并不难。使用这个简单的解析器语法，我能够自动化我的团队的部分工作量，例如，编写样板文件(并且容易出错)适配器，将 HTTP 请求转换成命令。这些简单的 DSL 让这变得微不足道。

看到上面的操作后，我希望您正在考虑所有可以用 DSL 定义和自动化的事情。那么为什么不写一个简单的 DSL 和解析器，并尝试一下呢？